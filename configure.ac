dnl required version of autoconf
AC_PREREQ([2.53])

dnl TODO: fill in your package name and package version here
AC_INIT([gst-plugins-dvswitch],[0.0.4])

dnl required versions of gstreamer and plugins-base
GST_0_10_REQUIRED=0.10.16
GSTPB_0_10_REQUIRED=0.10.16
GST_1_0_REQUIRED=1.2.0
GSTPB_1_0_REQUIRED=1.2.0

AC_ARG_ENABLE([gst_1_0], AS_HELP_STRING([--disable-gst_1_0],
  [Disable the usage of GStreamer 1.0, building against GStreamer 0.10 if installed]))

AC_CONFIG_SRCDIR([src/gstdvswitchsrc.c])
AC_CONFIG_HEADERS([config.h])
AC_CONFIG_MACRO_DIR([m4])

dnl required version of automake
AM_INIT_AUTOMAKE([1.10])

dnl enable mainainer mode by default
AM_MAINTAINER_MODE([enable])

dnl check for tools (compiler etc.)
AC_PROG_CC

dnl required version of libtool
LT_PREREQ([2.2.6])
LT_INIT

dnl give error and exit if we don't have pkgconfig
AC_CHECK_PROG(HAVE_PKGCONFIG, pkg-config, [ ], [
  AC_MSG_ERROR([You need to have pkg-config installed!])
])

dnl Check for the required version of GStreamer core (and gst-plugins-base)
dnl This will export GST_CFLAGS and GST_LIBS variables for use in Makefile.am
dnl
dnl If you need libraries from gst-plugins-base here, also add:
dnl for libgstaudio-1.0: gstreamer-audio-1.0 >= $GST_REQUIRED
dnl for libgstvideo-1.0: gstreamer-video-1.0 >= $GST_REQUIRED
dnl for libgsttag-1.0: gstreamer-tag-1.0 >= $GST_REQUIRED
dnl for libgstpbutils-1.0: gstreamer-pbutils-1.0 >= $GST_REQUIRED
dnl for libgstfft-1.0: gstreamer-fft-1.0 >= $GST_REQUIRED
dnl for libgstinterfaces-1.0: gstreamer-interfaces-1.0 >= $GST_REQUIRED
dnl for libgstrtp-1.0: gstreamer-rtp-1.0 >= $GST_REQUIRED
dnl for libgstrtsp-1.0: gstreamer-rtsp-1.0 >= $GST_REQUIRED
dnl etc.

AS_IF([test "x$enable_gst_1_0" == "xno"], [
  AC_MSG_WARN([GStreamer 1.0 forcefully disabled! Checking for GStreamer 0.10!])
  PKG_CHECK_MODULES([GST], [
    gstreamer-0.10 >= $GST_0_10_REQUIRED
    gstreamer-base-0.10 >= $GST_0_10_REQUIRED
    gstreamer-pbutils-0.10 >= $GSTPB_0_10_REQUIRED
  ], [
    AC_SUBST(GST_CFLAGS)
    AC_SUBST(GST_LIBS)
    GST_VER=0.10
  ], [
    AC_MSG_ERROR([
        You need to install or upgrade the GStreamer development
        packages on your system. On debian-based systems these are
        libgstreamer0.10-dev and libgstreamer-plugins-base0.10-dev.
        on RPM-based systems gstreamer0.10-devel, libgstreamer0.10-devel
        or similar.
    ])
  ])
  ], [
  dnl Check for gstreamer 1.0. If not found, check for gstreamer 0.10. If none found fail
  PKG_CHECK_MODULES([GST], [
    gstreamer-1.0 >= $GST_1_0_REQUIRED
    gstreamer-base-1.0 >= $GST_1_0_REQUIRED
    gstreamer-pbutils-1.0 >= $GSTPB_1_0_REQUIRED
  ], [
    AC_SUBST(GST_CFLAGS)
    AC_SUBST(GST_LIBS)
    GST_VER=1.0
  ], [
    AC_MSG_WARN([GStreamer 1.0 not found. Checking for GStreamer 0.10])
    PKG_CHECK_MODULES([GST], [
      gstreamer-0.10 >= $GST_0_10_REQUIRED
      gstreamer-base-0.10 >= $GST_0_10_REQUIRED
      gstreamer-pbutils-0.10 >= $GSTPB_0_10_REQUIRED
    ], [
      AC_SUBST(GST_CFLAGS)
      AC_SUBST(GST_LIBS)
      GST_VER=0.10
    ], [
      AC_MSG_ERROR([
          Neither GStreamer 0.10 nor GStreamer 1.0 could be found!
          You need to install or upgrade the GStreamer development
          packages on your system. On debian-based systems these are
          libgstreamer*-dev and libgstreamer-plugins-base*-dev.
          on RPM-based systems gstreamer*-devel, libgstreamer*-devel
          or similar.
      ])
    ])
  ])
])

dnl check if compiler understands -Wall (if yes, add -Wall to GST_CFLAGS)
AC_MSG_CHECKING([to see if compiler understands -Wall])
save_CFLAGS="$CFLAGS"
CFLAGS="$CFLAGS -Wall"
AC_COMPILE_IFELSE([AC_LANG_PROGRAM([ ], [ ])], [
  GST_CFLAGS="$GST_CFLAGS -Wall"
  AC_MSG_RESULT([yes])
], [
  AC_MSG_RESULT([no])
])

dnl set the plugindir where plugins should be installed (for src/Makefile.am)
if test "x${prefix}" = "x$HOME"; then
  plugindir="$HOME/.gstreamer-[$GST_VER]/plugins"
else
  plugindir="\$(libdir)/gstreamer-[$GST_VER]"
fi
AC_SUBST(plugindir)

dnl set proper LDFLAGS for plugins
GST_PLUGIN_LDFLAGS='-module -avoid-version -export-symbols-regex [_]*\(gst_\|Gst\|GST_\).*'
AC_SUBST(GST_PLUGIN_LDFLAGS)

AC_CONFIG_FILES([Makefile src/Makefile])
AC_OUTPUT

